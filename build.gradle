apply plugin: 'java'
apply plugin: 'eclipse'

sourceCompatibility = 1.7
targetCompatibility = 1.7
version = '1.0'

repositories {
    mavenCentral()
}

// Integration Test Structure
sourceSets {
    integTest {
        java.srcDir file('src/integTest/java')
    }
}

dependencies {
    compile group: 'commons-collections', name: 'commons-collections', version: '3.2'
    compile 'org.apache.commons:commons-math3:3.5'
    testCompile group: 'junit', name: 'junit', version: '4.+'
    testCompile 'pl.pragmatists:JUnitParams:1.0.4'
    
    integTestCompile sourceSets.main.output
    integTestCompile configurations.testCompile
    integTestCompile sourceSets.test.output
    integTestRuntime configurations.testRuntime
}

task integTest(type: Test) {
    testClassesDir = sourceSets.integTest.output.classesDir
    classpath = sourceSets.integTest.runtimeClasspath
}

jar {
    manifest {
        attributes 'Implementation-Title': 'Curumo Chess Engine',
                   'Implementation-Version': version,
                   'Main-Class': 'com.tdyck.uci.UCI'
    }
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}